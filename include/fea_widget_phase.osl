// fea_widget_phase osl libraries by Philippe Groarke
// Copyright 2022 Philippe Groarke, All rights reserved. This file is licensed under Apache 2.0 license
// https://github.com/ADN-DevTech/3dsMax-OSL-Shaders/blob/master/LICENSE.txt
#ifndef fea_widget_phase_osl
#define fea_widget_phase_osl
#include "D:\code\3dsmax-plugins\OSL\FeaOSL\include\fea_widget.osl"
#include "D:\code\3dsmax-plugins\OSL\FeaOSL\include\fea_hash.osl"

#define FEA_WGT_PHASE_INPUTS \
	FEA_SPACER(200), \
	FEA_TITLE(200, "Phase"), \
\
	int in_phase_mode = 2 \
	[[ \
		string widget = "mapper", \
		string label = "Phase Mode", \
		string options = \
			"Seed:2" \
			"|Time:0" \
			"|Phase Input:1" \
			"|Position + Seed:3" \
			, \
		string help = "Select the Phase input mode.<br>" \
			"Note, 'Position + Seed' only works in renderer." \
			, \
		int connectable = 0, \
	]], \
	float in_phase_time = 0.0 \
	[[ \
		string label = "Phase Time (s)", \
		string help = "The scene time in seconds. Piped into phase when using 'Time' mode.", \
		string widget = "null", \
		int timeValue = 1, \
		int connectable = 0, \
	]], \
	float in_phase_input = 0.0 \
	[[ \
		string label = "Phase Input", \
		string help = "Animates the shader, or used as a seed.", \
		string widget = "null", \
	]], \
	float in_phase_mul = 1.0 \
	[[ \
		string label = "Phase Multiplier", \
		string help = "Used to scale phase.", \
		int connectable = 0, \
	]], \
	float in_phase_offset = 0.0 \
	[[ \
		string label = "Phase Offset / Seed", \
		string help = "A fixed amount added to phase. When in 'Seed' mode, this is the value.", \
		int connectable = 0, \
	]]

float fea_wgt_compute_phase(int mode, float ptime, float phase, float mul,
		float offset) {
	float ret = 0.0;
	if (mode == 0) {
		// scene time
		ret = ptime * mul + offset;
	} else if (mode == 1) {
		// phase input
		ret = phase * mul + offset;
	} else if (mode == 2) {
		// seed
		ret = offset;
	} else if (mode == 3) {
		// position + seed
		point p = transform("object", "world", point(0));
		vector h = fea_hash3_osl(offset, p);
		ret = h[0] + h[1] + h[2] + offset;
	}
	return ret;
}

#define FEA_WGT_GET_PHASE \
	fea_wgt_compute_phase(in_phase_mode, in_phase_time, in_phase_input, \
			in_phase_mul, in_phase_offset)

#endif // fea_widget_phase_osl
