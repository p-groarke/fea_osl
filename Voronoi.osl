// Voronoi Shader
// Voronoi by Philippe Groarke
// Copyright 2022 Philippe Groarke, All rights reserved. This file is licensed under Apache 2.0 license
// https://github.com/ADN-DevTech/3dsMax-OSL-Shaders/blob/master/LICENSE.txt

#include "D:\code\3dsmax-plugins\OSL\FeaOSL\fea.osl"

// For more details on all this, see :
// https://iquilezles.org/articles/voronoilines/
// https://www.shadertoy.com/view/4lKGRG
// https://iquilezles.org/articles/smin/
// https://iquilezles.org/articles/smoothvoronoi/

// TODO : move to fea.osl

// // exponential smooth min (k=32)
// float smin( float a, float b, float k )
// {
//     float res = exp2( -k*a ) + exp2( -k*b );
//     return -log2( res )/k;
// }
// // power smooth min (k=8)
// float smin( float a, float b, float k )
// {
//     a = pow( a, k ); b = pow( b, k );
//     return pow( (a*b)/(a+b), 1.0/k );
// }
// // root smooth min (k=0.01)
// float smin( float a, float b, float k )
// {
//     float h = a-b;
//     return 0.5*( (a+b) - sqrt(h*h+k) );
// }
// // polynomial smooth min
// float sminCubic( float a, float b, float k )
// {
//     float h = max( k-abs(a-b), 0.0 )/k;
//     return min( a, b ) - h*h*h*k*(1.0/6.0);
// }



// Hash type enum ui options.
#define fea_hash_opts \
		"OSL:0" \
		"|IQ:1"

// Hashes the given vector p according to dimension and with hash type.
vector fea_hash(int dimensions, int hash_type, float seed, vector p) {
	if (dimensions == 1) {
		if (hash_type == 0) {
			return fea_hash1_osl(seed, p);
		} else if (hash_type == 1) {
			return fea_hash1_iq(seed, p);
		}
	} else if (dimensions == 2) {
		if (hash_type == 0) {
			return fea_hash2_osl(seed, p);
		} else if (hash_type == 1) {
			return fea_hash2_iq(seed, p);
		}
	} else if (dimensions == 3) {
		if (hash_type == 0) {
			return fea_hash3_osl(seed, p);
		} else if (hash_type == 1) {
			return fea_hash3_iq(seed, p);
		}
	}
	return vector(0);
}
vector fea_hash(int dimensions, int hash_type, vector p) {
	return fea_hash(dimensions, hash_type, 0.0, p);
}

// Ui for dimension options.
#define fea_dimension_opts \
		"1D:1" \
		"|2D:2" \
		"|3D:3"

#define fea_mk_rnd_pnts_iters 4
#define fea_mk_rnd_pnts_arr_size 64
#define fea_mk_rnd_pnts_off 1.5

// Given a coordinate 'x', generates a sample set of random points
// in the grid around the original coordinate.
//
// The points are in local space, relative to the input coordinate.
// That is, out point + x == world space point.
// And length(out point) == distance(x, random point).
//
// Specify the number of dimensions to compute, 1, 2 or 3.
// Provide the hash_func enum value (see fea_hash_func_opts).
// Provide a seed to randomize points. Note the seed is not periodic.
// Provide your coordinate (dimensions will be dealt with appropriately).
// Provide an array of at minimum size fea_mk_rnd_pnts_iters ^ dimension
void fea_make_random_points(int dimensions, int hash_type, float seed, point x, point out_points[]) {
	int arr_len = arraylength(out_points);
	if (arr_len < int(pow(fea_mk_rnd_pnts_iters, dimensions))) {
		return;
	}

	point mcoord = x;
	mcoord[1] = dimensions < 2 ? 0 : mcoord[1];
	mcoord[2] = dimensions < 3 ? 0 : mcoord[2];

	point abs_grid = floor(mcoord);
	vector lcl_shade_point = fract(mcoord);

	// Given a local grid coord, generate a random point local to the input coord.
	point make_rnd_point(float i, float j, float k) {
		// Offset to neighbour grid point.
		vector offset = vector(i, j, k);

		// Given an absolute point on our grid, returns a random direction.
		vector rand_dir = fea_hash(dimensions, hash_type, seed, abs_grid + offset);

		// The random point, local to grid point (abs_grid).
		point grid_rand_point = rand_dir + offset;

		// A vector from our shading point to the random point.
		// Aka, the random point local to our shading point.
		return grid_rand_point - lcl_shade_point;
	}
	// Search around our grid point, in 1d, 2d or 3d.
	int iters = fea_mk_rnd_pnts_iters;
	int iters2 = iters * iters;
	float off = fea_mk_rnd_pnts_off;
	int k_max = dimensions < 3 ? 1 : iters;
	float k_off = dimensions < 3 ? 0.0 : off;
	int j_max = dimensions < 2 ? 1 : iters;
	float j_off = dimensions < 2 ? 0.0 : off;

	for (int k = 0; k < k_max; ++k) {
		for (int j = 0; j < j_max; ++j) {
			for (int i = 0; i < iters; ++i) {
				int idx = k * iters2 + j * iters + i;
				out_points[idx] = make_rnd_point(
						float(i) - off,
						float(j) - j_off,
						float(k) - k_off
				);
			}
		}
	}
}

// fea_animate_points animation options
#define fea_anim_pts_opts \
		"None:0" \
		"|Circles:1" \
		"|Slosh:2" \
		"|Back and Forth:3" \
		"|Test:4"

// Animates an array of points (in local space to the shading coordinate).
// Offers various animations.
// Provide the hash type, phase, shading coordinate.
void fea_animate_points(int dimensions, int anim_opt, float anim_strength, int hash_type, float phase, point coord, output point pts[], int num_pts) {
	if (anim_opt == 0) {
		return;
	}

	// Phase in radians.
	float phase_rad = fea_per_to_rad(phase);

	for (int i = 0; i < num_pts; ++i) {
		point lcl_p = pts[i];

		// All our computations must be evaluated in global space.
		point p = lcl_p + coord;
		point stable_p = fea_floor_to(p, 100.0);
		vector rand_dir = fea_hash(dimensions, hash_type, stable_p) * 0.25 * anim_strength;

		if (anim_opt == 1) {
			// circles
			point other_p = p;
			other_p[0] -= rand_dir[2];
			other_p[1] -= rand_dir[0];
			other_p[2] -= rand_dir[1];
			p = rotate(p, phase_rad, p + rand_dir, other_p);
		} else if (anim_opt == 2) {
			// slosh
			p = rotate(p, phase_rad, p + rand_dir, point(0));
		} else if (anim_opt == 3) {
			// back and forth
			p += rand_dir * sin(phase_rad);
		} else if (anim_opt == 4) {
			// test
		}

		// Make local again.
		pts[i] = p - coord;
	}
}


// https://en.wikipedia.org/wiki/Taxicab_geometry
float fea_distance_manhattan(point p1, point p2) {
	float ret = 0.0;
	for (int i = 0; i < 3; ++i) {
		ret += abs(p1[i] - p2[i]);
	}
	return ret;
}

// https://en.wikipedia.org/wiki/Chebyshev_distance
float fea_distance_chebyshev(point p1, point p2) {
	float ret = abs(p1[0] - p2[0]);
	for (int i = 1; i < 3; ++i) {
		ret = max(ret, abs(p1[i] - p2[i]));
	}
	return ret;
}

point fea_mean(point pts[], int num_pts) {
	point ret;
	if (num_pts == 0) {
		return ret;
	}

	for (int i = 0; i < num_pts; ++i) {
		ret += pts[i];
	}

	return ret / float(num_pts);
}

// https://en.wikipedia.org/wiki/Mahalanobis_distance
float fea_distance_mahalanobis(point p1, point pts[], int num_pts) {
	point mean_point = fea_mean(pts, num_pts);

	// test
	return distance(p1, mean_point);
}

float fea_distance_test(point p1, point p2) {
	float denum = 0.0;
	for (int i = 0; i < 3; ++i) {
		denum += p1[i] + p2[i];
	}
	if (denum == 0.0) {
		return 0.0;
	}

	float num = fea_distance_manhattan(p1, p2);
	return num / denum;
}



// fea_voronoi distance options.
//
// Tested but not applicable : Canberra, Bray Curtis,
#define fea_distance_opts \
		"Default (Euclidean Squared):0" \
		/* "|Euclidean:1" */ \
		"|Manhattan:2" \
		"|Chebyshev:3" \
		"|Mahalanobis:4" \
		"|Test:5"

// fea_voronoi blending options.
#define fea_distance_smooth_opts \
		"None:0" \
		"|Metaballs:1" \
		"|Exponent:2" \
		"|Power:3"


// Given random points local to the currently shaded sample,
// returns the distance to the closest point (and its index).
// Aka, voronoi.
float fea_voronoi(int distance_opt, int blend_opt, float blend_strength, point pts[], int num_pts, output int out_idx) {
	// First, massage falloff according to dimensions and other settings,
	// so it is a reasonable value.
	float falloff = blend_strength;
	if (blend_opt == 1) {
		// metaballs
		if (falloff < 0.000001) {
			falloff = 0.000001;
		}
	} else if (blend_opt == 2) {
		// exponent
		falloff = 64.0 - (falloff * 64.0);
	} else if (blend_opt == 3) {
		// power
		falloff = 16.0 - (falloff * 16.0);
	}


	float min_dist = FEA_FLT_MAX;
	float blended_dist = 0.0;

	for (int i = 0; i < num_pts; ++i) {
		point current_pt = pts[i];
		float d = 0.0;


		// Compute the distance according to input option.
		// Note that, since we are in local space, our point is 0,0.
		if (distance_opt == 0 || distance_opt == 1) {
			// euclidean
			d = fea_length_squared(current_pt);
		} else if (distance_opt == 2) {
			// manhattan
			d = fea_distance_manhattan(point(0), current_pt);
		} else if (distance_opt == 3) {
			// chebyshev
			d = fea_distance_chebyshev(point(0), current_pt);
		} else if (distance_opt == 4) {
			// mahalanobis
			d = fea_distance_mahalanobis(point(0), pts, num_pts);
		} else if (distance_opt == 5) {
			// tests
		}

		// Whatever the metrics used, always store min to find closest point.
		if (d < min_dist) {
			min_dist = d;
			out_idx = i;
		}


		// If appropriate, compute blending.
		if (blend_opt == 1) {
			// metaballs
			if (distance_opt == 0) {
				blended_dist += pow(1.0 / d, 2.0);
			} else {
				blended_dist += pow(1.0 / (d * d), 2.0);
			}
		} else if (blend_opt == 2) {
			// exponent
			blended_dist += exp2(-falloff * d);
		} else if (blend_opt == 3) {
			// power
			blended_dist += 1.0 / pow(d, falloff);
		}
	}

	// Finally, return the expected value.
	if (blend_opt == 1) {
		// metaballs
		return (blended_dist / float(num_pts)) * falloff;
	} else if (blend_opt == 2) {
		// exponent
		return -(1.0 / falloff) * log2(blended_dist);
	} else if (blend_opt == 3) {
		// power
		return pow(1.0 / blended_dist, 1.0 / falloff);
	}

	return min_dist;
}

// Given random points local to the currently shaded coordinate,
// returns the distance to the closest border in between closeby points (voronoi).
// Also outputs the index of the point that was used (the second closest point).
//
// Provide the index of the closest point in the point set.
float fea_voronoi_borders(int closest_idx, point pts[], int num_pts, output int second_idx) {
	point closest_pt = pts[closest_idx];
	float ret = FEA_FLT_MAX;
	for (int i = 0; i < num_pts; ++i) {
		// Skip the closest point (we are looking for second closest border).
		if (i != closest_idx) {
			point current_pt = pts[i];
			float dist_to_brdr = dot(0.5 * (closest_pt + current_pt), normalize(current_pt - closest_pt));
			if (dist_to_brdr < ret) {
				ret = dist_to_brdr;
				second_idx = i;
			}
		}
	}

	return ret;
}


shader Voronoi
[[
	string help  =
		"<h3>Voronoi</h3>"
		"A voronoi / cell noise implementation."
		,
	string label = "Voronoi"
]]
(
	int in_dimensions = 3
	[[
		string label = "Dimensions",
		string help = "Evaluates the functions in 1d, 2d or 3d. The higher the dimension, the heavier the shader.<br>"
				"3d is particularly useful when rendering volumes, to eliminate seams or if your UVs contain the W component.",
		string widget = "mapper",
		string options =
			"2D:2"
			"|3D:3"
			,
		int connectable = 0,
	]],

	int in_hash_type = 0
	[[
		string label = "Hash Function",
		string help = "The core function used to generate the random points.",
		string widget = "mapper",
		string options =
			fea_hash_opts
			,
		int connectable = 0,
		string packName = "Hash Function / Seed",
	]],

	float in_seed = 0.0
	[[
		string label = "Seed",
		string help = "A seed provided to the hash function.",
		int connectable = 0,
		string packName = "Hash Function / Seed",
	]],


	int in_distance_type = 0
	[[
		string label = "Distance Metric",
		string help = "The distance function used to compute distance to points."
			"<br>Note, the euclidean is squared due to sqrt precision issues. You may take square root of the distance output if required.",
		string widget = "mapper",
		string options =
			fea_distance_opts
			,
		int connectable = 0,
	]],

	// FEA_SPACER(0),

	int in_cell_style = 0
	[[
		string label = "Cell Style",
		string help = "The style of the cell interior.",
		string widget = "mapper",
		string options =
			"Organic:0"
			"|Bubbles:1"
			"|Chiseled:2"
			"|Color:3"
			,
		int connectable = 0,
	]],

	int in_cell_smooth_type = 0
	[[
		string label = "Smoothing",
		string help = "Various options to compute the distance of one cell to another.",
		string widget = "mapper",
		string options =
			fea_distance_smooth_opts
			,
		int connectable = 0,
		string packName = "Smoothing / Strength",
	]],

	float in_cell_smooth_strength = 0.5
	[[
		string label = "Smoothing Strength",
		string help = "The strength of the cell blending.",
		float min = 0.0,
		float max = 1.0,
		// int connectable = 0,
		string packName = "Smoothing / Strength",
		int widgetWidth = FEA_RPACK_W,
	]],

	int in_threshold_type = 0
	[[
		string label = "Threshold",
		string help = "Thresholds the function using various schemes.",
		string widget = "mapper",
		string options =
			"None:0"
			"|Threshold:1"
			"|Slice:2"
			"|Contours:3"
			,
		int connectable = 0,
	]],

	float in_threshold_val = 0.5
	[[
		string label = "Threshold Value",
		string help = "The value of the threshold 'slice'.",
		float min = 0.0,
		float max = 1.0,
		// int connectable = 0,
		string packName = "Threshold Value / Feather",
	]],

	float in_threshold_feather = 0.1
	[[
		string label = "Threshold Feather",
		string help = "The edge smoothness of the threshold 'slice'.",
		float min = 0.0,
		float max = 1.0,
		// int connectable = 0,
		string packName = "Threshold Value / Feather",
	]],

	// FEA_SPACER(1)

	int in_draw_borders = 0
	[[
		string label = "Draw Borders",
		string help = "Render solid voronoi cell borders.",
		string widget = "checkBox",
		int connectable = 0,
	]],

	float in_border_width = 1.0
	[[
		string label = "Border Width",
		string help = "The cell border width.",
		float min = 0.0,
	]],

	float in_border_feather = 0.01
	[[
		string label = "Border Feather",
		string help = "The cell border fade amount.",
		float min = 0.001,
	]],

	color in_border_color = color(1)
	[[
		string label = "Border Color",
		string help = "The cell border color.",
	]],

	int in_animation_type = 0
	[[
		string label = "Animation",
		string help = "Animates the points using various schemes. Pipe the scene time in the phase input to animate.",
		string widget = "mapper",
		string options =
			fea_anim_pts_opts
			,
		int connectable = 0,
		string packName = "Animation / Strength",
	]],

	float in_animation_strength = 1.0
	[[
		string label = "Animation Strength",
		string help = "The range of animation. Note, for technical reasons, the range must remain quite limited.",
		float min = 0.0,
		float max = 1.0,
		int connectable = 0,
		string packName = "Animation / Strength",
		int widgetWidth = FEA_RPACK_W,
	]],

	float in_time = 0.0
	[[
		string label = "Time (s)",
		string help = "Used to animate, animation loops at 1.",
		int timeValue = 1,
	]],

	int in_clamp_output = 1
	[[
		string label = "Clamp Output",
		string help = "Clamps the output value between [0..1].",
		string widget = "checkBox",
		int connectable = 0,
	]],

	float in_uvw_scale = 1.0
	[[
		string label = "UVW Scale",
		string help = "Scale your UVWs up or down (or the object, if no UVWs are provided).<br>For more betterer transformations, use UVW Transform node.",
		float min = 0.00001,
		int connectable = 0,
	]],

	point in_uvw = transform("object", P)
	[[
		string label = "UVW",
		string help = "Transformed UVW coordinates. Animate 'w' with scene time for interesting results.",
	]],


	output color Out = 0,
	output vector Distance = 0
)
{

/*
	feedback
	1. blender doc of voronoi node (baseline, truly understand) cool stuff : write a color, use as function to drive
	2. additional parameter, a percentage of will i use a feature point or not.
		aka, 20% of the points

	border distortion in 3d (check iq stuff)
	smoothmin (to make border "rounded")
	modulate hash vector ( exposed)
	expose phase (animate points like iq)
	blur borders (check iq article)
	perdurbe distance of borders (makes border edges ragged)
	fractal of full function
	instead of doing distance, use blob calculation (metablobs!)
	bomb implicits (sphere == blobs, super quadratic, super ellipsoid)
		bomb random rotation

	explore not f2 - f1
	other map slot to do domain distortion
		distorter map

	crazy : gradient blur?

*/

	point coord = in_uvw / in_uvw_scale;
	coord[1] = in_dimensions < 2 ? 0 : coord[1];
	coord[2] = in_dimensions < 3 ? 0 : coord[2];

	// Contains max points required.
	point rnd_pts[fea_mk_rnd_pnts_arr_size];
	int num_pts = int(pow(fea_mk_rnd_pnts_iters, in_dimensions));

	// Generate a bunch of random points, local to shaded coordinate.
	fea_make_random_points(in_dimensions, in_hash_type, in_seed, coord, rnd_pts);

	// Animate them.
	fea_animate_points(in_dimensions, in_animation_type, in_animation_strength, in_hash_type, in_time, coord, rnd_pts, num_pts);

	// Find the closest distance to a point. Aka, voronoi.
	int closest_idx = -1;
	float dist = fea_voronoi(in_distance_type, in_cell_smooth_type, in_cell_smooth_strength, rnd_pts, num_pts, closest_idx);

	// Find the distance to the closest and second closest cell border.
	int border_idx = -1;
	float border_dist = fea_voronoi_borders(closest_idx, rnd_pts, num_pts, border_idx);

	// Internal cell style
	if (in_cell_style == 0) {
		// organic
		// nothing to do

		// float lines = fea_usin((coord[0] + coord[1]) * 10.0 + dist * 1.0);
		// dist = lines;
		// dist += lines;
	} else if (in_cell_style == 1) {
		// bubbles
		dist = 1.0 - dist;
	} else if (in_cell_style == 2) {
		// chiseled
		dist = border_dist;
	}

	if (in_threshold_type == 1) {
		// threshold
		dist = smoothstep(in_threshold_val - in_threshold_feather, in_threshold_val + in_threshold_feather, dist);
	} else if (in_threshold_type == 2) {
		// slice
		float diff = abs(in_threshold_val - dist);
		// diff = fea_interp(-10.0, diff);
		dist = 1.0 - smoothstep(0.0, in_threshold_feather, diff);
	} else if (in_threshold_type == 3) {
		// contours
		dist = clamp(dist, 0.0, 1.0);
		if (dist != 0.0 && dist != 1.0) {
			float d = fmod(dist, in_threshold_val);
			float f = in_threshold_feather * 0.1;
			dist = smoothstep(in_threshold_val - f, in_threshold_val, d);
			dist += 1.0 - smoothstep(0.0, f, d);
		} else {
			dist = 0.0;
		}
	}

	Out = dist;

	// TODO : color will be a seperate thing
	if (in_cell_style == 3) {
		// color, TODO
		// temp, make global coord
		Out = (coord + rnd_pts[closest_idx]) * 0.1;
	}

	// Borders
	if (in_draw_borders) {
		float w = in_border_width * 0.1;
		float eps = in_border_feather * 0.1;
		float p = 1.0 - smoothstep(w, w + eps, border_dist);
		Out = mix(Out, in_border_color, p);
	}

	if (in_clamp_output) {
		Out = clamp(Out, color(0), color(1));
	}
}